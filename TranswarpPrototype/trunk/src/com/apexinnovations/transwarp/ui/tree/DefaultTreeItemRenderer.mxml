<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				xmlns:assets="com.apexinnovations.transwarp.application.assets.*"
				autoDrawBackground="true"
				dataChange="update()"
				click="click()">
	
	<s:layout>
		<s:HorizontalLayout verticalAlign="middle" paddingTop="2" paddingBottom="2" paddingLeft="{indentation + 10}" paddingRight="20" gap="0"/>
	</s:layout>
	
	<assets:AssetImageControl assetID="disclosure" id="disclosure"/>
	<assets:AssetImageControl assetID="{iconName}" />
	
	<s:Label id="labelDisplay" fontWeight="{fontWeight}" paddingLeft="5"/>

	<fx:Script>
		<![CDATA[
			import com.apexinnovations.transwarp.application.events.TreeOpenCloseEvent;
			
			import spark.components.List;
			
			protected var _indentationGap:Number = 24;
			
			protected var isFolder:Boolean;
			protected var isOpen:Boolean = false;
						
			public function get indentationGap():Number { return _indentationGap; }
			public function set indentaionGap(value:Number):void {
				_indentationGap = value;
				update();
			}
						
			[Bindable] protected var iconName:String;
			[Bindable] protected var indentation:Number = 0;
			[Bindable] protected var fontWeight:String = "normal";

			
			protected function click():void {
				XML(data).@visited = "true";
				
				if(isFolder) {
					var list:XMLLinearIList = XMLLinearIList(List(owner).dataProvider);
					if(!isOpen)
						list.openNode(XML(data));
					else
						list.closeNode(XML(data));
					owner.dispatchEvent(new TreeOpenCloseEvent());
				}
				
				update();
			}
			
			
			protected function update():void {
				var xml:XML = XML(data);
				var list:XMLLinearIList = XMLLinearIList(List(owner).dataProvider);
								
				isOpen = list.openNodes.indexOf(xml) != -1; 
				isFolder = xml.children().length() > 0;
				
				disclosure.visible = isFolder;
				disclosure.rotation = isOpen ? 90 : 0;
				
				fontWeight = xml.@visited == "true" ? "normal" : "bold";
				indentation = (list.getDepth(xml) - 1) * _indentationGap;
				iconName = isFolder ? "folder" : (xml.@bookmarked == true ? (xml.@updated == true ? "updated-bookmarked" : "bookmarked") : (xml.@updated == true ? "updated" : "page"));
			}
		]]>
	</fx:Script>
	
	
	
</s:ItemRenderer>
